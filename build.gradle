group 'org.openbw.tsbw4j'
version ''

apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

sourceSets {
	main {
		output.classesDir = 'build/classes'
		output.resourcesDir = 'build/classes'
	}
    test {
		output.classesDir = 'build/classes'
		output.resourcesDir = 'build/classes'
	}
}

dependencies {
	compile files('../BWAPI4J/BWAPI4J/build/libs/BWAPI4J.jar')
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'co.paralleluniverse', name: 'quasar-core', version: '0.7.7', classifier: 'jdk8'
    compile group: 'co.paralleluniverse', name: 'quasar-actors', version: '0.7.7'
}

compileJava.dependsOn processResources
compileJava {
    doLast {
        ant.taskdef(name:'instrumentation', classname:'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: configurations.compile.asPath)
        ant.instrumentation(verbose:'true', check:'true', debug:'true') {
            fileset(dir: sourceSets.main.output.classesDir)
        }
        sourceSets.all {
		    println name
		}
        println 'AOT instrumentation: DONE'
    }
}

task fatJar(type: Jar) {
	manifest {
		attributes (
			'Main-Class': mainClassName,
			"Class-Path": configurations.compile.collect { it.getName() }.join(' ')
		)
    }
   
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
    doLast {
    	println 'fatJar: DONE'
    }
}
